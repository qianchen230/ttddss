.版本 2
.支持库 spec

.程序集 类_处理数据

.子程序 处理数据, , ,  
.参数 i, 整数型
.参数 封包数据, 字节集

.判断开始 (取字节集左边 (封包数据, 1) ＝ { 171 })
    _AB (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 178 })
    _B2 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 185 })
    _B9 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 181 })
    _B5 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 142 })
    _8E (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 173 })
    _AD (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 172 })
    _AC (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 174 })
    _AE (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 162 })
    _A2 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 183 })
    _B7 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 179 })
    _B3 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 180 })
    _B4 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 169 })
    _A9 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 161 })
    _A1 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 166 })
    _A6 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 187 })
    _BB (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 170 })
    _AA (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 160 })
    _A0 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 186 })
    _BA (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 168 })
    _A8 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 138 })
    _8A (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 189 })
    _BD (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 190 })
    _BE (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 165 })
    _A5 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 236 })
    _EC (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 166 })
    _A6 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 146 })
    _92 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 153 })
    _99 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 159 })
    _9F (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 152 })
    _98 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 135 })
    _87 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 228 })  ' BOSS
    _E4 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 182 })  ' 购买物品
    _B6 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 176 })
    _B0 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 139 })  ' 工作经验
    _8B (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 227 })  ' 本命灯
    _E3 (i, 封包数据)
.判断 (取字节集左边 (封包数据, 1) ＝ { 229 })  ' 转转乐
    _E5 (i, 封包数据)
.默认

.判断结束


.子程序 _AB, , , 对话开始
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 175 })  ' AF
    人物数据 [i].正在处理事件 ＝ 真

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 172 } 且 i ＞ 1)  ' AC
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 4))  ' id
    .如果真 (人物数据 [i].是否组队 ＝ 真 且 a ＝ 到整数 (控件_登陆_帐号 [1].内容))
        人物数据 [i].坐标.x ＝ 解密到数值 (取字节集中间 (封包数据, 8, 2))  ' x
        人物数据 [i].坐标.y ＝ 解密到数值 (取字节集中间 (封包数据, 10, 2))  ' y
        坐标改变 (i)
    .如果真结束

.默认

.判断结束


.子程序 _B9, , , 对话相关
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型
.局部变量 b, 整数型


.如果真 (i ＝ 1 且 主窗口.时钟_卡任务.时钟周期 ＞ 0)
    主窗口.时钟_卡任务.时钟周期 ＝ 0
.如果真结束

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 172 })
    ' b9,ac
    .如果真 (i ＝ 1 且 挂机配置.开始挂机 且 挂机配置.正在挂机)
        返回 ()
    .如果真结束

    .如果真 (个人配置 [i].煮水饺 或 个人配置 [i].煮鱼料理 或 个人配置 [i].煮菜 或 个人配置 [i].蒸年糕 或 个人配置 [i].自动合成物品 或 配置_刷灯笼.flag 或 人物数据 [i].兑换灯笼 或 人物数据 [i].贩卖物品 或 人物数据 [i].进活动地图)  ' 此处用来屏蔽对话响应,那边发包直接完成
        返回 ()
    .如果真结束

    a ＝ 解密到数值 (取字节集中间 (封包数据, 7, 1))
    b ＝ 解密到数值 (取字节集右边 (封包数据, 2))
    .判断开始 (挂机配置.开始挂机 且 人物数据 [i].坐标.mapID ＝ 挂机配置.挂机地图)

    .判断 (a ＝ 6)
        .判断开始 (主窗口.动作脚本_运行脚本.选中)
            人物数据 [i].任务选择计数 ＝ 人物数据 [i].任务选择计数 ＋ 1
            .判断开始 (动作脚本.购买物品 且 人物数据 [i].任务选择计数 ＝ 2)
                .判断开始 (动作脚本.脚本指令 ＝ “购买小宠”)
                    发送封包 (i, 十六到字节集 (“59,E9,A8,AD,B6,AC,” ＋ 数值加密 (8 ＋ 人物数据 [i].属性, 1) ＋ 数值加密 (动作脚本.参数_整数1, 2) ＋ “59,E9,AE,AD,B9,A4,85,59,E9,AF,AD,B9,AB”))

                .默认
                    发送封包 (i, 动作脚本.封包数据)  ' 发送买物品封包，并且关闭对话框
                .判断结束

            .默认
                b ＝ 核心_对话选择 (人物数据 [i].任务选择计数)
                .判断开始 (b ＝ 0 且 动作脚本.购买物品)
                    发送封包 (i, 动作脚本.封包数据)

                .默认
                    显示信息 (i, “选择: ” ＋ 到文本 (b), #蓝色)
                    发送封包 (i, 十六到字节集 (“59,E9,AE,AD,B9,A4,” ＋ 数值加密 (b ＋ 29)))
                    左键点击 (i)
                .判断结束

            .判断结束

        .判断 (人物数据 [i].钱庄打开)
            发送封包 (i, 十六到字节集 (“59,E9,AE,AD,B9,A4,B2”))
            左键点击 (i)

        .判断 (人物数据 [i].客栈打开)
            发送封包 (i, 十六到字节集 (“59,E9,AE,AD,B9,A4,8D”))
            左键点击 (i)

        .默认
            发送封包 (i, 十六到字节集 (“59,E9,AE,AD,B9,A4,B3,59,E9,AF,AD,B9,AB”))  ' 默认选择1
        .判断结束

    .默认
        左键点击 (i)
        .如果真 (b ＞ 0)
            显示信息 (i, 取对话内容 (b), #墨绿)
        .如果真结束

    .判断结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 160 })
    ' b9,a0
    .如果真 (挂机配置.正在挂机 且 人物数据 [i].坐标.mapID ＝ 挂机配置.挂机地图)
        显示信息 (i, “B9,A0 请联系作者.讲明是刷什么任务出现的这个.有没有过任务.”)
    .如果真结束
    .如果真 (人物数据 [i].正在切图 ＝ 假)
        左键点击 (i)
    .如果真结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 165 })  ' B9,A5 事件结束
    .如果真 (主窗口.时钟_卡任务.时钟周期 ＞ 0)
        主窗口.时钟_卡任务.时钟周期 ＝ 0
    .如果真结束
    事件结束 (i)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ 十六到字节集 (“A3”) 且 挂机配置.正在挂机 ＝ 假)  ' B9,A3
    左键点击 (i)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ 十六到字节集 (“A6”) 且 挂机配置.正在挂机 ＝ 假)  ' B9,A6
    左键点击 (i)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ 十六到字节集 (“A7”) 且 挂机配置.正在挂机 ＝ 假)  ' B9,A7
    左键点击 (i)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ 十六到字节集 (“BC”) 且 挂机配置.正在挂机 ＝ 假)  ' B9,BC 收惊婆答题末尾出现
    左键点击 (i)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ 十六到字节集 (“BD”) 且 挂机配置.正在挂机 ＝ 假)  ' B9,BD
    左键点击 (i)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ 十六到字节集 (“AA”))  ' B9,AA
    ' 切换地图的时候出现
    ' 左键点击 (i)
    ' 人物数据 [i].正在处理事件 ＝ 真
.判断 (取字节集中间 (封包数据, 2, 1) ＝ 十六到字节集 (“84”))  ' B9,84
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))
    .判断开始 (a ＝ 2)
        显示信息 (i, “抢救江东二乔活动已经开始了喔,请各位玩家赶快动身前往邺城铜雀台展开救援行动吧!”, #红色)
    .判断 (a ＝ 3)
        显示信息 (i, “抢救江东二乔活动已经结束咯,欢迎下次再来挑战.”, #红色)
    .默认

    .判断结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ 十六到字节集 (“98”))  ' B9,98
    .判断开始 (取字节集中间 (封包数据, 3, 1) ＝ 十六到字节集 (“AC”))
        .判断开始 (解密到数值 (取字节集中间 (封包数据, 4, 1)) ＝ 0)
            .判断开始 (个人配置 [i].煮水饺)
                煮水饺_检查材料 (i)
            .默认

            .判断结束

        .默认
            显示信息 (i, “开始煮菜...”, #墨绿)
            人物数据 [i].正在煮菜 ＝ 真
        .判断结束

    .判断 (取字节集中间 (封包数据, 3, 1) ＝ 十六到字节集 (“AF”))
        显示信息 (i, “停止煮菜...”, #红色)
        人物数据 [i].正在煮菜 ＝ 假
        .判断开始 (个人配置 [i].煮水饺)
            煮水饺_ (i)
        .判断 (个人配置 [i].煮鱼料理)
            煮鱼料理_ (i)
        .判断 (个人配置 [i].煮菜)
            煮菜_ (i)
        .判断 (个人配置 [i].蒸年糕)
            蒸年糕_ (i)
        .默认

        .判断结束

    .默认

    .判断结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ 十六到字节集 (“9B”))  ' B9,9B
    .判断开始 (取字节集中间 (封包数据, 3, 2) ＝ 十六到字节集 (“A8,AC”))
        ' 59,E9,AB,AD,B9,9B,A8,AC,F4,AD
        显示信息 (i, “任务剩余时间: ” ＋ 到文本 (解密到数值 (取字节集右边 (封包数据, 2))) ＋ “分钟.”, #红色)
    .默认

    .判断结束

.默认

.判断结束


.子程序 _B2, , , 客栈住宿
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 type, 整数型
.局部变量 index, 整数型
.局部变量 length, 整数型

type ＝ 解密到数值 (取字节集中间 (封包数据, 2, 1))

.判断开始 (type ＝ 1)  ' B2,AC
    左键点击 (i)  ' 住宿结束

.判断 (type ＝ 2)  ' B2,AF
    发送封包 (i, 十六到字节集 (“59,E9,AF,AD,B2,AC”))  ' 确认住宿

.判断 (type ＝ 4)  ' B2,A9
    index ＝ 解密到数值 (取字节集右边 (封包数据, 1))
    人物数据 [i].客栈武将 [index].id ＝ 0
    人物数据 [i].客栈武将 [index].lv ＝ 0
    人物数据 [i].客栈武将 [index].hp ＝ 0
    人物数据 [i].客栈武将 [index].name ＝ “”
    客栈_加载武将 ()

.判断 (type ＝ 6)  ' B2,AB 加载客栈武将
    ' 59,E9,B3,AD,B2,AB,AC,FA,EB,AC,74,AD,AB,05,07,05,72,1D,05,AF,45,1D,C7,CA,AA,A5,05,CA,0C,E8,1B,E4,6F,60
    封包数据 ＝ 取字节集右边 (封包数据, 取字节集长度 (封包数据) － 2)
    .判断循环首 (取字节集长度 (封包数据) ＞ 0)
        index ＝ 解密到数值 (取字节集左边 (封包数据, 1))
        length ＝ 解密到数值 (取字节集中间 (封包数据, 7, 1))  ' 名字长度
        人物数据 [i].客栈武将 [index].id ＝ 解密到数值 (取字节集中间 (封包数据, 2, 2))
        人物数据 [i].客栈武将 [index].lv ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))
        人物数据 [i].客栈武将 [index].hp ＝ 解密到数值 (取字节集中间 (封包数据, 5, 2))
        人物数据 [i].客栈武将 [index].name ＝ 解密到文本 (取字节集中间 (封包数据, 8, length))
        封包数据 ＝ 取字节集右边 (封包数据, 取字节集长度 (封包数据) － 7 － length)
    .判断循环尾 ()
    客栈_加载武将 ()

.判断 (type ＝ 7)  ' B2,AA
    人物数据 [i].客栈打开 ＝ 真
    显示信息 (i, “客栈武将已打开...”, #蓝色)
    刷新客栈按钮 ()
.默认

.判断结束


.子程序 _B3, , , 钱庄物品加载
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 类型, 整数型
.局部变量 a, 整数型
.局部变量 临时数据, 数据类型_物品栏
.局部变量 b, 整数型

类型 ＝ 解密到数值 (取字节集中间 (封包数据, 2, 1))
.判断开始 (类型 ＝ 1 或 类型 ＝ 9)
    封包数据 ＝ 字节集替换 (封包数据, 1, 2, )
    .判断开始 (类型 ＝ 1)
        .计次循环首 (取字节集长度 (封包数据) ÷ 12, )
            a ＝ 解密到数值 (取字节集左边 (封包数据, 1))
            人物数据 [i].钱庄 [a].id ＝ 解密到数值 (取字节集中间 (封包数据, 2, 2))
            人物数据 [i].钱庄 [a].数量 ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))
            人物数据 [i].钱庄 [a].损耗 ＝ 解密到数值 (取字节集中间 (封包数据, 5, 1))
            人物数据 [i].钱庄 [a].附加属性 ＝ 解密到数值 (取字节集中间 (封包数据, 6, 1))
            人物数据 [i].钱庄 [a].附加属性值 ＝ 解密到数值 (取字节集中间 (封包数据, 7, 1))
            封包数据 ＝ 字节集替换 (封包数据, 1, 12, )
        .计次循环尾 ()

    .默认
        .计次循环首 (取字节集长度 (封包数据) ÷ 12, )
            a ＝ 解密到数值 (取字节集左边 (封包数据, 1))
            人物数据 [i].钱庄2 [a].id ＝ 解密到数值 (取字节集中间 (封包数据, 2, 2))
            人物数据 [i].钱庄2 [a].数量 ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))
            人物数据 [i].钱庄2 [a].损耗 ＝ 解密到数值 (取字节集中间 (封包数据, 5, 1))
            人物数据 [i].钱庄2 [a].附加属性 ＝ 解密到数值 (取字节集中间 (封包数据, 6, 1))
            人物数据 [i].钱庄2 [a].附加属性值 ＝ 解密到数值 (取字节集中间 (封包数据, 7, 1))
            封包数据 ＝ 字节集替换 (封包数据, 1, 12, )
        .计次循环尾 ()

    .判断结束

.判断 (类型 ＝ 2 或 类型 ＝ 10)  ' 钱庄获得
    临时数据.id ＝ 解密到数值 (取字节集中间 (封包数据, 3, 2))
    临时数据.数量 ＝ 解密到数值 (取字节集中间 (封包数据, 5, 1))
    临时数据.损耗 ＝ 解密到数值 (取字节集中间 (封包数据, 6, 1))
    临时数据.附加属性 ＝ 解密到数值 (取字节集中间 (封包数据, 7, 1))
    临时数据.附加属性值 ＝ 解密到数值 (取字节集中间 (封包数据, 8, 1))
    .如果真 (可否堆叠 (临时数据.id) ＝ 真)
        .判断开始 (类型 ＝ 2)
            .计次循环首 (50, a)
                .如果真 (人物数据 [i].钱庄 [a].id ＝ 临时数据.id)
                    .判断开始 (人物数据 [i].钱庄 [a].数量 ＋ 临时数据.数量 ＞ 50)
                        临时数据.数量 ＝ 临时数据.数量 － (50 － 人物数据 [i].钱庄 [a].数量)
                        人物数据 [i].钱庄 [a].数量 ＝ 50
                    .默认
                        人物数据 [i].钱庄 [a].数量 ＝ 人物数据 [i].钱庄 [a].数量 ＋ 临时数据.数量
                        临时数据.数量 ＝ 0
                    .判断结束

                .如果真结束

                .如果真 (临时数据.数量 ＝ 0)
                    跳出循环 ()
                .如果真结束

            .计次循环尾 ()

        .默认
            .计次循环首 (50, a)
                .如果真 (人物数据 [i].钱庄2 [a].id ＝ 临时数据.id)
                    .判断开始 (人物数据 [i].钱庄2 [a].数量 ＋ 临时数据.数量 ＞ 50)
                        临时数据.数量 ＝ 临时数据.数量 － (50 － 人物数据 [i].钱庄2 [a].数量)
                        人物数据 [i].钱庄2 [a].数量 ＝ 50
                    .默认
                        人物数据 [i].钱庄2 [a].数量 ＝ 人物数据 [i].钱庄2 [a].数量 ＋ 临时数据.数量
                        临时数据.数量 ＝ 0
                    .判断结束

                .如果真结束

                .如果真 (临时数据.数量 ＝ 0)
                    跳出循环 ()
                .如果真结束

            .计次循环尾 ()

        .判断结束

    .如果真结束

    .如果真 (临时数据.数量 ＞ 0)
        .判断开始 (类型 ＝ 2)
            .计次循环首 (50, a)
                .如果真 (人物数据 [i].钱庄 [a].id ＝ 0)
                    人物数据 [i].钱庄 [a] ＝ 临时数据
                    跳出循环 ()
                .如果真结束

            .计次循环尾 ()

        .默认
            .计次循环首 (50, a)
                .如果真 (人物数据 [i].钱庄2 [a].id ＝ 0)
                    人物数据 [i].钱庄2 [a] ＝ 临时数据
                    跳出循环 ()
                .如果真结束

            .计次循环尾 ()

        .判断结束

    .如果真结束
    刷新钱庄物品列表 (i)

.判断 (类型 ＝ 5 或 类型 ＝ 11)  ' 钱庄物品消失 5=第一页 11=第二页
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))  ' 位置
    b ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))  ' 数量
    .判断开始 (类型 ＝ 5)
        .判断开始 (人物数据 [i].钱庄 [a].数量 ＞ b)
            人物数据 [i].钱庄 [a].数量 ＝ 人物数据 [i].钱庄 [a].数量 － b

        .默认
            人物数据 [i].钱庄 [a] ＝ 临时数据
        .判断结束

    .判断 (人物数据 [i].钱庄2 [a].数量 ＞ b)
        人物数据 [i].钱庄2 [a].数量 ＝ 人物数据 [i].钱庄2 [a].数量 － b

    .默认
        人物数据 [i].钱庄2 [a] ＝ 临时数据
    .判断结束
    刷新钱庄物品列表 (i)
.默认

.判断结束



.子程序 _B5, , , 福神衰神
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型
.局部变量 b, 整数型
.局部变量 c, 整数型

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 165 })  ' A5
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 4))  ' id
    .如果真 (a ＝ 人物数据 [i].id)
        b ＝ 解密到数值 (取字节集中间 (封包数据, 7, 1))  ' 类型
        c ＝ 解密到数值 (取字节集右边 (封包数据, 1))  ' 值
        .判断开始 (b ＝ 1)
            人物数据 [i].衰神数量 ＝ c
            刷新状态条 (i, 2)
        .判断 (b ＝ 2)
            人物数据 [i].福神数量 ＝ c
            刷新状态条 (i, 1)
        .默认

        .判断结束

    .如果真结束

.默认

.判断结束


.子程序 _8E, , , 剩余储值点数
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 169 })  ' A9    剩余点数
    a ＝ 解密到数值 (取字节集中间 (封包数据, 7, 4))
    显示信息 (i, “储值点数:” ＋ 到文本 (a), #品红)
.默认

.判断结束


.子程序 _AD, , , 连接断开信息
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型

a ＝ 解密到数值 (取字节集右边 (封包数据, 1))
.判断开始 (a ＝ 13)
    显示信息 (i, “服务器断开连接:” ＋ 到文本 (a) ＋ “:当前事件没有结束时触发新事件.”, #红色)
.判断 (a ＝ 14)
    显示信息 (i, “服务器断开连接:” ＋ 到文本 (a) ＋ “:移动距离过大断线.”, #红色)
.判断 (a ＝ 58)
    显示信息 (i, “服务器断开连接:” ＋ 到文本 (a) ＋ “:目前服务器人数过多请稍后,或至其他服务器进行游戏.”, #红色)
.判断 (a ＝ 19)
    显示信息 (i, “服务器断开连接:” ＋ 到文本 (a) ＋ “:重复登陆断线.”, #红色)
.判断 (a ＝ 59)
    显示信息 (i, “服务器断开连接:” ＋ 到文本 (a) ＋ “:角色搁置过久,请重新登陆.”, #红色)
.判断 (a ＝ 60)
    显示信息 (i, “服务器断开连接:” ＋ 到文本 (a) ＋ “:单一IP连接数已达上线.”, #红色)

.判断 (a ＝ 37)
    显示信息 (i, “服务器断开连接:” ＋ 到文本 (a) ＋ “:超距离点击NPC”, #红色)

.默认
    显示信息 (i, “服务器断开连接:” ＋ 到文本 (a), #红色)
.判断结束


.子程序 _AC, , , 断线
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 172 })  ' AC,AC 玩家下线
    a ＝ 解密到数值 (取字节集右边 (封包数据, 4))
    玩家下线 (i, a)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 171 })  ' AB
    显示信息 (i, “密码错误.”, #红色)
    断开连接 (i)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 164 })  ' A4
    封包数据 ＝ 十六到字节集 (“AC,” ＋ 数值加密 (取文本长度 (控件_登陆_密码 [i].内容), 1) ＋ 数值加密 (到整数 (控件_登陆_帐号 [i].内容), 4) ＋ 文本加密 (多项选择 (主窗口.登陆_地区.现行选中项 ＋ 1, “wn”, “wp”, “tl”, “tp”, “wf”)) ＋ 数值加密 (多项选择 (主窗口.登陆_地区.现行选中项 ＋ 1, 209, 209, 209, 209, 188), 2) ＋ 文本加密 (控件_登陆_密码 [i].内容))
    封包数据 ＝ { 89, 233 } ＋ 十六到字节集 (数值加密 (取字节集长度 (封包数据), 2)) ＋ 封包数据
    发送封包 (i, 封包数据)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 174 })  ' AE
    显示信息 (i, “此帐号无角色.”, #红色)
    断开连接 (i)
.默认

.判断结束


.子程序 _AE, , , 玩家上线
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型
.局部变量 b, 整数型
.局部变量 c, 整数型
.局部变量 d, 整数型

' AE
' 2(4)=id   6=性别   c+6=转生等级   9(2)=地图ID   11(2)=坐标x   13(2)=坐标y    26=装备件数
a ＝ 解密到数值 (取字节集中间 (封包数据, 2, 4))
.判断开始 (a ＝ 人物数据 [i].id)
    b ＝ 解密到数值 (取字节集中间 (封包数据, 9, 2))
    c ＝ 26 ＋ 解密到数值 (取字节集中间 (封包数据, 26, 1)) × 2
    人物数据 [i].名字 ＝ 解密到文本 (取字节集右边 (封包数据, 取字节集长度 (封包数据) － (c ＋ 7)))
    人物数据 [i].转生 ＝ 解密到数值 (取字节集中间 (封包数据, c ＋ 6, 1))
    人物数据 [i].衰神数量 ＝ 解密到数值 (取字节集中间 (封包数据, 7, 1))
    人物数据 [i].福神数量 ＝ 解密到数值 (取字节集中间 (封包数据, 8, 1))
    人物数据 [i].坐标.mapID ＝ b
    人物数据 [i].坐标.mapName ＝ 取地图名 (b, 真)
    人物数据 [i].坐标.x ＝ 解密到数值 (取字节集中间 (封包数据, 11, 2))
    人物数据 [i].坐标.y ＝ 解密到数值 (取字节集中间 (封包数据, 13, 2))

    .如果真 (i ＝ 1)
        加入成员 (队伍_成员, 人物数据 [i].id)
    .如果真结束

    .如果真 (人物数据 [i].转生 ＞ 1)
        d ＝ 解密到数值 (取字节集中间 (封包数据, c ＋ 7, 1))
        .判断开始 (d ＞ 4)
            人物数据 [i].转生职业 ＝ “未知”
        .默认
            人物数据 [i].转生职业 ＝ “  [ ” ＋ 多项选择 (d, “侠”, “霸”, “贤”, “仙”) ＋ “ ]”
        .判断结束

    .如果真结束
    刷新地图NPC (i)

.默认
    玩家上线 (i, 封包数据)
.判断结束


.子程序 _A2, , , 武将相关
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型
.局部变量 b, 整数型
.局部变量 c, 整数型
.局部变量 临时武将信息, 武将数据类型

' A2
.判断开始 (封包数据 ＝ { 162, 190, 175 })  ' 对方拒绝接收快递
    ' A2,BE
    显示信息 (i, “对方拒绝接收快递”, #红色)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 167 })  ' 马车武将
    ' A2,A7
    封包数据 ＝ 取字节集右边 (封包数据, 取字节集长度 (封包数据) － 2)
    .判断循环首 (取字节集长度 (封包数据) ＞ 0)
        a ＝ 解密到数值 (取字节集左边 (封包数据, 1))  ' 序号
        人物数据 [i].马车武将 [a].id ＝ 解密到数值 (取字节集中间 (封包数据, 2, 2))
        人物数据 [i].马车武将 [a].lv ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))
        人物数据 [i].马车武将 [a].hp ＝ 解密到数值 (取字节集中间 (封包数据, 5, 2))
        b ＝ 解密到数值 (取字节集中间 (封包数据, 7, 1))  ' 名字长度
        人物数据 [i].马车武将 [a].name ＝ 解密到文本 (取字节集中间 (封包数据, 8, b))
        封包数据 ＝ 取字节集右边 (封包数据, 取字节集长度 (封包数据) － 7 － b)
    .判断循环尾 ()
    .如果真 (主窗口.选择夹_主.现行子夹 ＝ i 且 窗口_马车客栈.可视)
        马车_加载武将 ()
    .如果真结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 171 })  ' 下马
    ' A2,AB
    a ＝ 解密到数值 (取字节集右边 (封包数据, 4))
    .如果真 (a ＝ 人物数据 [i].id)
        人物数据 [i].骑马状态 ＝ 假
        显示信息 (i, “骑马: 取消骑马”, #红色)
    .如果真结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 168 })  ' 上马
    ' A2,A8
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 4))
    .如果真 (a ＝ 人物数据 [i].id)
        b ＝ 解密到数值 (取字节集中间 (封包数据, 7, 4))
        人物数据 [i].骑马状态 ＝ 真
        .计次循环首 (4, c)
            .如果真 (人物数据 [i].武将 [c].id ＝ b)
                显示信息 (i, “骑马: ” ＋ 人物数据 [i].武将 [c].名字 ＋ “[” ＋ 到文本 (人物数据 [i].武将 [c].id) ＋ “]”, #蓝色)
                返回 ()
            .如果真结束

        .计次循环尾 ()
    .如果真结束


.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 172 })  ' 获得武将加入
    ' A2,AC
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 4))  ' 人物ID
    b ＝ 解密到数值 (取字节集中间 (封包数据, 7, 1))  ' 序号
    c ＝ 解密到数值 (取字节集中间 (封包数据, 8, 4))  ' 武将ID
    .如果真 (人物数据 [i].id ＝ a)
        显示信息 (i, “获得武将: ” ＋ 取NPC名 (c, 真), #蓝色)
        .如果真 (人物数据 [i].武将 [b].id ＝ 0)
            获得武将 (i, b, c)
        .如果真结束

        .如果真 (窗口_马车客栈.可视)
            身上_加载武将 ()
        .如果真结束

    .如果真结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 175 })  ' 武将离开
    ' A2,AF
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 4))  ' 人物ID
    .如果真 (a ＝ 人物数据 [i].id)
        b ＝ 解密到数值 (取字节集右边 (封包数据, 1))  ' 武将序号
        a ＝ 人物数据 [i].武将 [b].id
        显示信息 (i, “失去武将: ” ＋ 人物数据 [i].武将 [b].名字 ＋ “(” ＋ 到文本 (a) ＋ “)”, #红色)
        人物数据 [i].武将 [b] ＝ 临时武将信息
        .判断开始 (a ＝ 人物数据 [i].巴豆妖ID)
            人物数据 [i].巴豆妖ID ＝ 0
            .计次循环首 (4, a)
                .计次循环首 (取数组成员数 (巴豆妖ID), b)
                    .如果真 (巴豆妖ID [b] ＝ 人物数据 [i].武将 [a].id)
                        人物数据 [i].巴豆妖ID ＝ 人物数据 [i].武将 [a].id
                        显示信息 (i, “可供快递的巴豆妖:” ＋ 人物数据 [i].武将 [a].名字 ＋ “[” ＋ 到文本 (人物数据 [i].武将 [a].id) ＋ “]”, #蓝色)
                        跳出循环 ()
                    .如果真结束

                .计次循环尾 ()

            .计次循环尾 ()

        .判断 (人物数据 [i].出战武将序号 ＝ b)
            出战武将信息 (i, 0)
        .默认

        .判断结束

        .如果真 (主窗口.选择夹_主.现行子夹 ＝ i 且 窗口_马车客栈.可视)
            身上_加载武将 ()
        .如果真结束

    .如果真结束


.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 165 })
    ' A2,A5
    .如果真 (人物数据 [i].已连接 ＝ 假)
        显示信息 (i, “正在加载武将信息...”, #墨绿)
    .如果真结束
    加载武将 (i, 取字节集右边 (封包数据, 取字节集长度 (封包数据) － 2))
    身上_加载武将 ()

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 166 })  ' 马车武将提出
    ' A2,A6
    a ＝ 解密到数值 (取字节集右边 (封包数据, 1))
    人物数据 [i].马车武将 [a].id ＝ 0
    人物数据 [i].马车武将 [a].lv ＝ 0
    人物数据 [i].马车武将 [a].hp ＝ 0
    人物数据 [i].马车武将 [a].name ＝ “”
    .如果真 (主窗口.选择夹_主.现行子夹 ＝ i 且 窗口_马车客栈.可视)
        马车_加载武将 ()
    .如果真结束


.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 185 })  ' 武将 强化 属性读取
    ' A2,B9
    调试输出 (字节集到十六 (封包数据, “,”))
    封包数据 ＝ 取字节集右边 (封包数据, 取字节集长度 (封包数据) － 2)
    .判断开始 (主窗口.登陆_地区.现行选中项 ＝ 4)
        c ＝ 取字节集长度 (封包数据) ÷ 3
        .计次循环首 (c, a)
            b ＝ 解密到数值 (取字节集中间 (封包数据, a × 3 － 2, 1))
            人物数据 [i].武将 [b].健体琼浆 ＝ 解密到数值 (取字节集中间 (封包数据, a × 3 － 1, 1))
            人物数据 [i].武将 [b].幻能玉液 ＝ 解密到数值 (取字节集中间 (封包数据, a × 3, 1))
        .计次循环尾 ()
    .默认
        c ＝ 取字节集长度 (封包数据) ÷ 5
        .计次循环首 (c, a)
            b ＝ 解密到数值 (取字节集中间 (封包数据, a × 3 － 2, 1))
            人物数据 [i].武将 [b].健体琼浆 ＝ 解密到数值 (取字节集中间 (封包数据, a × 3 － 1, 1))
            人物数据 [i].武将 [b].幻能玉液 ＝ 解密到数值 (取字节集中间 (封包数据, a × 3, 1))
        .计次循环尾 ()
        封包数据 ＝ 取字节集右边 (封包数据, c × 2)
        .计次循环首 (c, a)
            b ＝ 解密到数值 (取字节集中间 (封包数据, a × 2 － 1, 1))
            .如果真 (b ＞ 0)
                人物数据 [i].武将 [b].强化 ＝ 解密到数值 (取字节集中间 (封包数据, a × 2, 1))
                .如果真 (人物数据 [i].武将 [b].强化 ＞ 0)
                    人物数据 [i].武将 [b].skill [5].id ＝ 取武将技能 (人物数据 [i].武将 [b].id, 5)
                    人物数据 [i].武将 [b].skill [5].lv ＝ 1
                .如果真结束

            .如果真结束

        .计次循环尾 ()
    .判断结束

.默认

.判断结束


.子程序 _B7, , , 金币相关
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 172 })  ' 获得金币
    ' B7,AC
    a ＝ 解密到数值 (取字节集右边 (封包数据, 4))
    .如果真 (a ＞ 0)
        人物数据 [i].金币 ＝ 人物数据 [i].金币 ＋ a
        显示信息 (i, “金币+” ＋ 到文本 (a), #蓝色)
    .如果真结束

    .如果真 (i ＝ 1 且 动作脚本.正在执行 且 a ≥ 动作脚本.参数_整数1)
        显示信息 (1, “索取金币动作完成!”, #蓝色)
        动作脚本.正在执行 ＝ 假
    .如果真结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 175 })  ' 减少金币
    ' B7,AF
    a ＝ 解密到数值 (取字节集右边 (封包数据, 4))
    .如果真 (a ＞ 0)
        人物数据 [i].金币 ＝ 人物数据 [i].金币 － a
        显示信息 (i, “金币-” ＋ 到文本 (a), #红色)
    .如果真结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 169 })  ' 加载金币
    ' B7,A9
    a ＝ 解密到数值 (取字节集右边 (封包数据, 8))
    人物数据 [i].金币 ＝ a
    显示信息 (i, “金币数量:” ＋ 到文本 (a), #蓝色)
.默认

.判断结束


.子程序 _B4, , , 交易相关
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 数据, 字节集
.局部变量 临时物品栏数据, 数据类型_物品栏
.局部变量 a, 整数型
.局部变量 b, 整数型
.局部变量 c, 整数型

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 172 })  ' 申请交易
    ' B4,AC
    a ＝ 解密到数值 (取字节集右边 (封包数据, 4))
    人物数据 [i].正在交易物品 ＝ 真
    人物数据 [i].交易_物品.id ＝ a
    显示信息 (i, “正与 ” ＋ 取玩家名 (i, a, 真) ＋ “ 进行物品交易.”, #蓝色)
    .判断开始 (主窗口.动作脚本_运行脚本.选中)
        .判断开始 (动作脚本.脚本指令 ＝ “索要物品”)
            .判断开始 (i ＝ 1)
                发送封包 (i, 十六到字节集 (“59,E9,AB,AD,B4,AF,AD,AD,AD,AD,59,E9,AE,AD,B4,AE,AC”))  ' 交易确定
                动作脚本.正在执行 ＝ 假

            .默认
                发送封包 (i, 动作脚本.封包数据)
            .判断结束

        .判断 (动作脚本.脚本指令 ＝ “索要金币”)
            .判断开始 (i ＝ 1)
                发送封包 (i, 十六到字节集 (“59,E9,AB,AD,B4,AF,AD,AD,AD,AD,59,E9,AE,AD,B4,AE,AC”))  ' 交易确定
                动作脚本.正在执行 ＝ 假

            .默认
                发送封包 (i, 动作脚本.封包数据)
            .判断结束

        .判断 (动作脚本.脚本指令 ＝ “交易物品”)
            .判断开始 (i ＝ 1)
                .计次循环首 (25, a)
                    .计次循环首 (取数组成员数 (自动交易列表), b)
                        .如果真 (人物数据 [i].物品栏 [a].id ＝ 自动交易列表 [b])
                            数据 ＝ 数据 ＋ 十六到字节集 (数值加密 (a, 1))
                            显示信息 (i, “交易出 ” ＋ 到文本 (a) ＋ “.” ＋ 取物品名 (人物数据 [i].物品栏 [a].id, 真) ＋ “ x” ＋ 到文本 (人物数据 [i].物品栏 [a].数量))
                            跳出循环 ()
                        .如果真结束

                    .计次循环尾 ()
                .计次循环尾 ()
                .判断开始 (取字节集长度 (数据) ＞ 0)
                    数据 ＝ 十六到字节集 (“B4,AF,AD,AD,AD,AD,”) ＋ 数据
                    数据 ＝ 十六到字节集 (“59,E9,” ＋ 数值加密 (取字节集长度 (数据), 2)) ＋ 数据 ＋ 十六到字节集 (“59,E9,AE,AD,B4,AE,AC”)
                    发送封包 (i, 数据)
                    ' 显示信息 (i, 字节集到十六 (数据, “,”))
                    动作脚本.正在执行 ＝ 假

                .默认
                    发送封包 (i, 十六到字节集 (“59,E9,AB,AD,B4,AF,AD,AD,AD,AD,59,E9,AE,AD,B4,AE,AC”))  ' 交易确定
                .判断结束


            .默认
                发送封包 (i, 十六到字节集 (“59,E9,AB,AD,B4,AF,AD,AD,AD,AD,59,E9,AE,AD,B4,AE,AC”))  ' 交易确定
            .判断结束

        .默认
            发送封包 (i, 十六到字节集 (“59,E9,AB,AD,B4,AF,AD,AD,AD,AD,59,E9,AE,AD,B4,AE,AC”))  ' 交易确定
        .判断结束


    .默认
        窗口_交易_物品.可视 ＝ 真
        窗口_交易_物品.选择夹_交易物品.现行子夹 ＝ i － 1
        _选择夹_交易物品_子夹被改变 ()
    .判断结束
    ' ' 发送封包 (i, 十六到字节集 (“59,E9,AB,AD,B4,AF,AD,AD,AD,AD,59,E9,AE,AD,B4,AE,AC”))

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 175 })  ' 交易结束  AF
    ' B4,AF
    .计次循环首 (25, a)
        人物数据 [i].交易_物品.对方物品 [a] ＝ 临时物品栏数据
    .计次循环尾 ()
    人物数据 [i].正在交易物品 ＝ 假
    显示信息 (i, “交易结束.”, #蓝色)
    刷新交易物品 (i)
    .计次循环首 (5, a)
        .如果真 (人物数据 [a].正在交易物品)
            返回 ()
        .如果真结束

    .计次循环尾 ()
    窗口_交易_物品.可视 ＝ 假

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 174 })  ' 交易信息   AE
    a ＝ (取字节集长度 (封包数据) － 6) ÷ 11
    ' B4,AE
    .计次循环首 (a, b)
        人物数据 [i].交易_物品.对方物品 [b].id ＝ 解密到数值 (取字节集中间 (封包数据, b × 11 － 4, 2))  ' id
        人物数据 [i].交易_物品.对方物品 [b].数量 ＝ 解密到数值 (取字节集中间 (封包数据, b × 11 － 2, 1))  ' 数量
        人物数据 [i].交易_物品.对方物品 [b].损耗 ＝ 解密到数值 (取字节集中间 (封包数据, b × 11 － 1, 1))  ' 耗损
    .计次循环尾 ()
    刷新交易物品 (i)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ 十六到字节集 (“A1”))  ' B4,A1 武将信息
    a ＝ 解密到数值 (取字节集中间 (封包数据, 18, 1))
    人物数据 [i].交易_武将.name ＝ 解密到文本 (取字节集中间 (封包数据, 19, a))
    人物数据 [i].交易_武将.lv ＝ 解密到数值 (取字节集中间 (封包数据, 9, 1))
    人物数据 [i].交易_武将.忠诚 ＝ 解密到数值 (取字节集中间 (封包数据, 18 ＋ a ＋ 1, 1))

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 167 })  ' B4,A7 交易武将
    人物数据 [i].正在交易武将 ＝ 真
    a ＝ 解密到数值 (取字节集右边 (封包数据, 4))
    人物数据 [i].交易_武将.id ＝ a
    显示信息 (i, “正与 ” ＋ 取玩家名 (i, a, 真) ＋ “ 进行武将交易.”, #蓝色)
    .判断开始 (主窗口.动作脚本_运行脚本.选中)
        .判断开始 (i ＝ 1)
            .判断开始 (动作脚本.脚本指令 ＝ “归还武将”)
                发送封包 (i, 动作脚本.封包数据)
            .默认
                发送封包 (i, 十六到字节集 (“59,E9,AA,AD,B4,A6,AD,AD,AD,AD,AD,59,E9,AE,AD,B4,A1,AC,”))  ' 交易确定
            .判断结束

        .判断 (动作脚本.脚本指令 ＝ “归还武将”)
            发送封包 (i, 十六到字节集 (“59,E9,AA,AD,B4,A6,AD,AD,AD,AD,AD,59,E9,AE,AD,B4,A1,AC,”))  ' 交易确定
        .默认
            发送封包 (i, 动作脚本.封包数据)
        .判断结束

    .默认
        窗口_交易_武将.可视 ＝ 真
        窗口_交易_武将.选择夹_交易武将.现行子夹 ＝ i － 1
        _选择夹_交易武将_子夹被改变 ()
    .判断结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 166 })  ' 59,E9,AE,AD,B4,A6,A9 交易结束
    人物数据 [i].正在交易武将 ＝ 假
    人物数据 [i].交易_武将.id ＝ 0
    人物数据 [i].交易_武将.lv ＝ 0
    a ＝ 解密到数值 (取字节集右边 (封包数据, 1))
    .判断开始 (a ＝ 1)
        显示信息 (i, “对方断线,无法交易.”, #红色)
    .判断 (a ＝ 2)
        显示信息 (i, “对方忙碌中,无法交易.”, #红色)
    .判断 (a ＝ 3)
        显示信息 (i, “对方取消交易.”, #红色)
    .判断 (a ＝ 4)
        显示信息 (i, “双方交易完成.”, #红色)
    .判断 (a ＝ 5)
        显示信息 (i, “双方不同场景,无法交易.”, #红色)
    .判断 (a ＝ 6)
        显示信息 (i, “某方金钱不足,取消交易.”, #红色)
    .判断 (a ＝ 7)
        显示信息 (i, “自己或是对方身上有重复的武将,取消交易.”, #红色)
    .判断 (a ＝ 8)
        显示信息 (i, “战斗中无法交易.”, #红色)
    .判断 (a ＝ 9)
        显示信息 (i, “武将资料比对有异,取消交易.”, #红色)
    .判断 (a ＝ 10)
        显示信息 (i, “身上最多只能带4个武将,取消交易.”, #红色)
    .判断 (a ＝ 11)
        显示信息 (i, “自己或对方有重复的武将在客栈,取消交易.”, #红色)
    .判断 (a ＝ 12)
        显示信息 (i, “武将忠诚为零,不可交易.”, #红色)
    .判断 (a ＝ 13)
        显示信息 (i, “自己或对方有重复的武将在马车,取消交易.”, #红色)
    .判断 (a ＝ 14)
        显示信息 (i, “武将身上有不可转让的物品.”, #红色)
    .判断 (a ＝ 15)
        显示信息 (i, “武将有霸魂状态,不可交易.”, #红色)
    .默认

    .判断结束
    .计次循环首 (5, a)
        .如果真 (人物数据 [a].正在交易武将)
            返回 ()
        .如果真结束

    .计次循环尾 ()
    窗口_交易_武将.可视 ＝ 假

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 185 })  ' 快递
    ' B4,B9,
    a ＝ 解密到数值 (取字节集右边 (封包数据, 1))
    .判断开始 (a ＝ 1)
        快递开始 (i)
    .判断 (a ＝ 2)
        快递结束 (i)
    .默认

    .判断结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 186 })  ' 快递信息
    ' B4,BA
    .判断开始 (取字节集中间 (封包数据, 3, 1) ＝ { 172 })  ' 送来
        ' AC
        a ＝ 解密到数值 (取字节集中间 (封包数据, 4, 4))
        b ＝ 解密到数值 (取字节集中间 (封包数据, 8, 2))
        c ＝ 解密到数值 (取字节集右边 (封包数据, 1))
        显示信息 (i, 取玩家名 (i, a, 真) ＋ “ 送来 ” ＋ 取物品名 (b, 真) ＋ “ ” ＋ 到文本 (c) ＋ “个.”, #蓝色)
    .判断 (取字节集中间 (封包数据, 3, 1) ＝ { 175 })  ' 送走
        ' AF
        a ＝ 解密到数值 (取字节集中间 (封包数据, 4, 4))
        b ＝ 解密到数值 (取字节集中间 (封包数据, 8, 2))
        c ＝ 解密到数值 (取字节集右边 (封包数据, 1))
        显示信息 (i, “已成功快递给 ” ＋ 取玩家名 (i, a, 真) ＋ “ ” ＋ 取物品名 (b, 真) ＋ “ ” ＋ 到文本 (c) ＋ “个.”, #蓝色)
    .默认

    .判断结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 187 })  ' 快递失败
    ' B4,BB
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))
    .判断开始 (a ＝ 1)  ' AC
        显示信息 (i, “数量不足,取消快递”, #红色)
    .判断 (a ＝ 2)  ' AF
        显示信息 (i, “对方不在线上,取消快递”, #红色)
    .判断 (a ＝ 3)  ' AE
        显示信息 (i, “对方物品空间不足,取消快递”, #红色)
    .判断 (a ＝ 4)  ' A9
        显示信息 (i, “对方在地牢中,取消快递”, #红色)
    .判断 (a ＝ 5)  ' A8
        显示信息 (i, “在地牢中,取消快递”, #红色)
    .判断 (a ＝ 6)  ' AB
        显示信息 (i, “比对资料有异常,取消快递”, #红色)
    .默认

    .判断结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 132 })  ' 84 -----登陆成功.
    ' B4,84
    显示状态 (i, 2)
    显示信息 (i, “登陆成功.”, #深青)
    显示信息 (i, “耗时:” ＋ 到文本 (四舍五入 ((取启动时间 () － 登陆时间 [i]) ÷ 1000, 2)) ＋ “ s.”, #红色)
    登陆成功 (i)
.默认

.判断结束


.子程序 _A9, , , 加载已在线玩家
.参数 i, 整数型
.参数 封包数据, 字节集

玩家上线 (i, 封包数据)

.子程序 _A1, , , 切换地图
.参数 i, 整数型
.参数 封包数据, 字节集

切换地图 (i, 封包数据)

.子程序 _A6, , , 小宠剩余时间,战斗开始，结束
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 时间, 日期时间型
.局部变量 a, 整数型
.局部变量 b, 整数型
.局部变量 c, 整数型

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 166 })  ' A6
    封包数据 ＝ 解密到字节集 (取字节集右边 (封包数据, 8))
    时间 ＝ 取字节集数据 (封包数据, #日期时间型, )
    显示信息 (i, “小宠剩余时间：” ＋ 到文本 (取整 (取字节集数据 (封包数据, #双精度小数型, ))) ＋ “天 ” ＋ 到文本 (取小时 (时间)) ＋ “:” ＋ 到文本 (取分钟 (时间)) ＋ “:” ＋ 到文本 (取秒 (时间)), #品红)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 87 })  ' 57 战场加载
    ' 显示信息 (i, “加载战场:” ＋ 到文本 (a))
    a ＝ 解密到数值 (取字节集中间 (封包数据, 5, 1))
    .判断开始 (a ＝ 4)  ' A9 观战
        人物数据 [i].观战 ＝ 真

    .默认
        战场_加载 (i, 字节集替换 (封包数据, 1, 4, ))
    .判断结束
    战斗开始 (i)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 168 })  ' 进入战场
    ' A6,A8
    战场_进入 (i, 封包数据)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 173 })  ' A6,AD 结束战斗

    .如果真 (人物数据 [i].正在战斗)
        a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 4))
        .判断开始 (a ＝ 人物数据 [i].id)
            战场_清理 (i)
            战斗结束 (i)

        .默认
            b ＝ 人物数据 [i].战场位置.x ＋ 1
            .计次循环首 (5, c)
                .如果真 (c ≠ 人物数据 [i].战场位置.y ＋ 1 且 人物数据 [i].战场 [b] [c].id ＝ a)
                    战场_离开 (i, b, c)
                    返回 ()
                .如果真结束

            .计次循环尾 ()
        .判断结束

    .如果真结束


.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 172 })  ' 人员离开战场
    ' A6,AC
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1)) ＋ 1
    b ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1)) ＋ 1
    战场_离开 (i, a, b)
.默认

.判断结束


.子程序 _BB, , , 野怪相关
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型
.局部变量 b, 整数型
.局部变量 c, 整数型

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 172 })  ' AC

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 175 })  ' AF 怪物移动
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))
    b ＝ 解密到数值 (取字节集中间 (封包数据, 5, 2))
    c ＝ 解密到数值 (取字节集中间 (封包数据, 7, 2))
    怪物移动 (i, a, b, c)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 174 })  ' AE 死亡
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))  ' 序号
    b ＝ 解密到数值 (取字节集右边 (封包数据, 2))  ' 复活计时 多加1秒
    怪物死亡 (i, a, b)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 168 })  ' A8 复活坐标
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))
    b ＝ 解密到数值 (取字节集中间 (封包数据, 5, 2))
    c ＝ 解密到数值 (取字节集中间 (封包数据, 7, 2))
    怪物移动 (i, a, b, c)
.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 169 })  ' A9 切图刷新NPC坐标
    封包数据 ＝ 字节集替换 (封包数据, 1, 2, )
    刷新NPC坐标 (i, 封包数据)
.默认

.判断结束


.子程序 _AA, , , 加载当前地图中的玩家
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型

a ＝ 解密到数值 (取字节集中间 (封包数据, 2, 4))
玩家进入地图 (i, a, 取玩家名 (i, a, 真))

.子程序 _A0, , , 组队相关
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型
.局部变量 b, 整数型

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 172 } 且 i ＝ 1)  ' a0,ac 申请入伍
    a ＝ 解密到数值 (取字节集右边 (封包数据, 4))
    显示信息 (i, 取玩家名 (i, a, 真) ＋ “ 申请加入队伍!”, #蓝色)
    .计次循环首 (4, b)
        .如果真 (a ＝ 到整数 (控件_登陆_帐号 [b ＋ 1].内容))
            发送封包 (i, { 89, 233, 170, 173, 160, 174, 172 } ＋ 十六到字节集 (数值加密 (a, 4)))
            ' 59,E9,AA,AD,A0,AE,AC,
            返回 ()
        .如果真结束

    .计次循环尾 ()

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 164 })  ' a0,a4 邀请队伍
    a ＝ 解密到数值 (取字节集右边 (封包数据, 4))
    显示信息 (i, 取玩家名 (i, a) ＋ “ 邀请加入队伍?”, #蓝色)
    .如果真 (a ＝ 到整数 (控件_登陆_帐号 [1].内容))
        ' 59,E9,AA,AD,A0,A5,AC,
        发送封包 (i, { 89, 233, 170, 173, 160, 165, 172 } ＋ 十六到字节集 (数值加密 (a, 4)))
    .如果真结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 168 })  ' a0,a8 加入队伍
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 4))  ' 加入的队长id
    .如果真 (a ＝ 到整数 (控件_登陆_帐号 [1].内容))  ' 判断是否是自己加入的队伍
        b ＝ 解密到数值 (取字节集右边 (封包数据, 4))  ' 加入的队伍成员id
        .判断开始 (i ＝ 1)
            人物数据 [i].是否组队 ＝ 真
            队伍_调整 (b, 真)
            显示信息 (i, 取玩家名 (i, b, 真) ＋ “ 加入队伍.”, #蓝色)
            显示信息 (i, “队伍人数:” ＋ 到文本 (取数组成员数 (队伍_成员)))
            .如果真 (到整数 (控件_登陆_帐号 [2].内容) ＝ b)
                发送封包 (i, { 89, 233, 171, 173, 160, 168 } ＋ 十六到字节集 (数值加密 (b, 4)))  ' 设置军师  59,E9,AB,AD,A0,A8
                显示信息 (i, 取玩家名 (i, b, 真) ＋ “ 成为军师.”, #蓝色)
            .如果真结束

            .如果真 (取反 (主窗口.动作脚本_运行脚本.选中))
                .判断开始 (挂机配置.开始挂机)
                    检测挂机 ()

                .判断 (主窗口.选择框_组队后自动运行.选中 且 取数组成员数 (队伍_成员) ≥ 挂机配置.队伍人数)
                    主窗口.挂机_开始任务.选中 ＝ 真
                    _挂机_开始任务_被单击 ()
                .默认

                .判断结束

            .如果真结束


        .判断 (b ＝ 人物数据 [i].id)
            人物数据 [i].是否组队 ＝ 真
            人物数据 [i].队长id ＝ a
            显示信息 (i, “你加入了 ” ＋ 取玩家名 (i, a, 真) ＋ “ 的队伍.”, #蓝色)
        .默认

        .判断结束

    .如果真结束



.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 169 })  ' a0,a9 离开队伍
    a ＝ 解密到数值 (取字节集右边 (封包数据, 4))  ' 离开队伍人员的id
    .判断开始 (i ＝ 1)
        队伍_调整 (a, 假)

    .判断 (人物数据 [i].是否组队 ＝ 真 且 a ＝ 人物数据 [i].队长id)
        人物数据 [i].是否组队 ＝ 假
        显示信息 (i, 取玩家名 (i, a, 真) ＋ “ 解散了队伍.”, #红色)

    .判断 (a ＝ 人物数据 [i].id)
        人物数据 [i].是否组队 ＝ 假
        显示信息 (i, “您已脱离 ” ＋ 取玩家名 (i, 人物数据 [i].队长id, 真) ＋ “ 的队伍.”, #红色)
    .默认

    .判断结束

.判断 (取字节集中间 (封包数据, 2, 2) ＝ { 174, 174 })  ' AE,AE    无回应
    显示信息 (i, “组队无回应”, #红色)
.判断 (取字节集中间 (封包数据, 2, 2) ＝ { 160, 174 })  ' A0,AE    忙碌
    显示信息 (i, “组队忙碌”, #红色)
.默认

.判断结束


.子程序 _BA, , , 物品相关
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 临时装备信息, 数据类型_物品栏, , "6"
.局部变量 b, 整数型
.局部变量 a, 整数型
.局部变量 c, 整数型
.局部变量 d, 整数型
.局部变量 e, 整数型
.局部变量 f, 整数型

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 166 })  ' 索引装备
    ' BA,A6
    封包数据 ＝ 字节集替换 (封包数据, 1, 2, )
    .计次循环首 (取字节集长度 (封包数据) ÷ 10, )
        b ＝ 解密到数值 (取字节集左边 (封包数据, 2))
        a ＝ 取装备位置 (b)
        .如果 (a ＞ 0)
            临时装备信息 [a].id ＝ b
            临时装备信息 [a].损耗 ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))
            临时装备信息 [a].附加属性 ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))
            临时装备信息 [a].附加属性值 ＝ 解密到数值 (取字节集中间 (封包数据, 5, 1)) － 100
            临时装备信息 [a].经验值 ＝ 解密到数值 (取字节集中间 (封包数据, 7, 4))
            临时装备信息 [a].属性 ＝ 取装备属性 (b)
            临时装备信息 [a].属性值 ＝ 取装备属性值 (b)
            临时装备信息 [a].lv ＝ 取物品等级 (b)
            临时装备信息 [a].数量 ＝ 1
        .否则
            显示信息 (i, “_BA 加载装备信息出错:” ＋ 取物品名 (b, 真), #红色)
        .如果结束

        封包数据 ＝ 字节集替换 (封包数据, 1, 10, )
    .计次循环尾 ()
    人物数据 [i].装备 ＝ 临时装备信息
    显示人物装备 (i)


.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 168 })  ' 索引物品栏物品
    ' BA,A8
    封包数据 ＝ 字节集替换 (封包数据, 1, 2, )
    .计次循环首 (取字节集长度 (封包数据) ÷ 12, )
        a ＝ 解密到数值 (取字节集左边 (封包数据, 1))
        人物数据 [i].物品栏 [a].id ＝ 解密到数值 (取字节集中间 (封包数据, 2, 2))
        人物数据 [i].物品栏 [a].数量 ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))
        人物数据 [i].物品栏 [a].损耗 ＝ 解密到数值 (取字节集中间 (封包数据, 5, 1))
        人物数据 [i].物品栏 [a].附加属性 ＝ 解密到数值 (取字节集中间 (封包数据, 6, 1))
        人物数据 [i].物品栏 [a].附加属性值 ＝ 解密到数值 (取字节集中间 (封包数据, 7, 1)) － 100
        人物数据 [i].物品栏 [a].经验值 ＝ 解密到数值 (取字节集中间 (封包数据, 9, 4))
        人物数据 [i].物品栏 [a].属性 ＝ 取装备属性 (人物数据 [i].物品栏 [a].id)
        人物数据 [i].物品栏 [a].属性值 ＝ 取装备属性值 (人物数据 [i].物品栏 [a].id)
        人物数据 [i].物品栏 [a].lv ＝ 取物品等级 (人物数据 [i].物品栏 [a].id)
        封包数据 ＝ 字节集替换 (封包数据, 1, 12, )
    .计次循环尾 ()
    刷新物品栏列表 (i)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 130 })  ' 索引背包巴豆物品
    ' BA,82
    封包数据 ＝ 字节集替换 (封包数据, 1, 2, )
    .计次循环首 (取字节集长度 (封包数据) ÷ 12, )
        a ＝ 解密到数值 (取字节集左边 (封包数据, 1))
        人物数据 [i].背包栏 [a].id ＝ 解密到数值 (取字节集中间 (封包数据, 2, 2))
        人物数据 [i].背包栏 [a].数量 ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))
        人物数据 [i].背包栏 [a].损耗 ＝ 解密到数值 (取字节集中间 (封包数据, 5, 1))
        人物数据 [i].背包栏 [a].附加属性 ＝ 解密到数值 (取字节集中间 (封包数据, 6, 1))
        人物数据 [i].背包栏 [a].附加属性值 ＝ 解密到数值 (取字节集中间 (封包数据, 7, 1)) － 100
        人物数据 [i].背包栏 [a].经验值 ＝ 解密到数值 (取字节集中间 (封包数据, 9, 4))
        人物数据 [i].背包栏 [a].属性 ＝ 取装备属性 (人物数据 [i].背包栏 [a].id)
        人物数据 [i].背包栏 [a].属性值 ＝ 取装备属性值 (人物数据 [i].背包栏 [a].id)
        人物数据 [i].背包栏 [a].lv ＝ 取物品等级 (人物数据 [i].背包栏 [a].id)
        封包数据 ＝ 字节集替换 (封包数据, 1, 12, )
    .计次循环尾 ()
    刷新背包栏列表 (i)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 133 })
    ' BA,85 武将清流信息
    .如果真 (个人配置 [i].显示清流)
        a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))  ' 武将序号
        b ＝ 解密到数值 (取字节集中间 (封包数据, 4, 2))  ' hp
        c ＝ 解密到数值 (取字节集中间 (封包数据, 6, 2))  ' sp
        显示信息 (i, 人物数据 [i].武将 [a].名字 ＋ “回复 HP:” ＋ 到文本 (b) ＋ “ SP:” ＋ 到文本 (c))
    .如果真结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 136 })
    ' BA,88 人物清流信息
    .如果真 (个人配置 [i].显示清流)
        b ＝ 解密到数值 (取字节集中间 (封包数据, 7, 4))
        .如果真 (b ＝ 人物数据 [i].id)
            a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 4))
            c ＝ 解密到数值 (取字节集中间 (封包数据, 11, 2))
            d ＝ 解密到数值 (取字节集中间 (封包数据, 13, 2))
            显示信息 (i, 取玩家名 (i, a, 真) ＋ “帮” ＋ 取玩家名 (i, b, 真) ＋ “回复 HP:” ＋ 到文本 (c) ＋ “ SP:” ＋ 到文本 (d))
        .如果真结束

    .如果真结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 169 })  ' 索引地图物品
    ' BA,A9


.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 170 })  ' 失去物品
    ' BA,AA
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 2))  ' 失去物品id
    b ＝ 解密到数值 (取字节集右边 (封包数据, 1))
    c ＝ 1
    .判断循环首 (b ＞ 0)
        .如果真 (人物数据 [i].物品栏 [c].id ＝ a)
            .判断开始 (b ＞ 人物数据 [i].物品栏 [c].数量)
                b ＝ b － 人物数据 [i].物品栏 [c].数量
                物品_失去 (i, c, 人物数据 [i].物品栏 [c].数量)

            .默认
                物品_失去 (i, c, b)
                返回 ()
            .判断结束

        .如果真结束

        c ＝ c ＋ 1
    .判断循环尾 ()

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 171 })  ' 得到物品
    ' BA,AB
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 2))  ' id
    b ＝ 解密到数值 (取字节集中间 (封包数据, 5, 1))  ' 数量
    e ＝ 解密到数值 (取字节集中间 (封包数据, 7, 1))  ' 损耗
    c ＝ 解密到数值 (取字节集中间 (封包数据, 8, 1))  ' 打毛的属性
    d ＝ 解密到数值 (取字节集中间 (封包数据, 9, 1)) － 100  ' 属性值-100为实际属性值
    f ＝ 解密到数值 (取字节集中间 (封包数据, 11, 4))  ' 经验值
    物品_获得 (i, , a, b, c, d, e)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 165 })  ' 得到物品
    ' BA,A5
    物品_获得 (i, 解密到数值 (取字节集中间 (封包数据, 3, 1)), 解密到数值 (取字节集中间 (封包数据, 4, 2)), 解密到数值 (取字节集中间 (封包数据, 6, 1)), 解密到数值 (取字节集中间 (封包数据, 8, 1)), 解密到数值 (取字节集中间 (封包数据, 9, 1)) － 100, 解密到数值 (取字节集中间 (封包数据, 7, 1)), 解密到数值 (取字节集中间 (封包数据, 11, 4)))

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 32 })  ' 炼化属性
    ' BA,20
    .判断开始 (取字节集中间 (封包数据, 3, 1) ＝ 十六到字节集 (“AC”))  ' 物品栏灵石
        封包数据 ＝ 取字节集右边 (封包数据, 取字节集长度 (封包数据) － 3)
        .判断循环首 (取字节集长度 (封包数据) ＞ 0)
            a ＝ 解密到数值 (取字节集左边 (封包数据, 1))  ' 索引位置
            人物数据 [i].物品栏 [a].炼化属性 ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))
            人物数据 [i].物品栏 [a].炼化等级 ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))
            封包数据 ＝ 字节集替换 (封包数据, 1, 9, )
        .判断循环尾 ()

    .判断 (取字节集中间 (封包数据, 3, 1) ＝ 十六到字节集 (“AF”))  ' 背包栏灵石
        封包数据 ＝ 取字节集右边 (封包数据, 取字节集长度 (封包数据) － 3)
        .判断循环首 (取字节集长度 (封包数据) ＞ 0)
            a ＝ 解密到数值 (取字节集左边 (封包数据, 1))  ' 索引位置
            人物数据 [i].背包栏 [a].炼化属性 ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))
            人物数据 [i].背包栏 [a].炼化等级 ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))
            封包数据 ＝ 字节集替换 (封包数据, 1, 9, )
        .判断循环尾 ()

    .判断 (取字节集中间 (封包数据, 3, 1) ＝ 十六到字节集 (“A5”))
        .判断开始 (取字节集中间 (封包数据, 4, 1) ＝ 十六到字节集 (“AD”))  ' 装备灵石
            封包数据 ＝ 取字节集右边 (封包数据, 取字节集长度 (封包数据) － 4)
            .判断循环首 (取字节集长度 (封包数据) ＞ 0)
                a ＝ 解密到数值 (取字节集左边 (封包数据, 1))  ' 索引位置
                人物数据 [i].装备 [a].炼化属性 ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))
                人物数据 [i].装备 [a].炼化等级 ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))
                封包数据 ＝ 字节集替换 (封包数据, 1, 9, )
            .判断循环尾 ()
        .默认

        .判断结束

    .判断 (取字节集中间 (封包数据, 3, 1) ＝ 十六到字节集 (“A8”))  ' 钱庄2灵石
        封包数据 ＝ 取字节集右边 (封包数据, 取字节集长度 (封包数据) － 3)
        .判断循环首 (取字节集长度 (封包数据) ＞ 0)
            a ＝ 解密到数值 (取字节集左边 (封包数据, 1))  ' 索引位置
            人物数据 [i].钱庄2 [a].炼化属性 ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))
            人物数据 [i].钱庄2 [a].炼化等级 ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))
            封包数据 ＝ 字节集替换 (封包数据, 1, 9, )
        .判断循环尾 ()

    .判断 (取字节集中间 (封包数据, 3, 1) ＝ 十六到字节集 (“A9”))  ' 钱庄灵石
        封包数据 ＝ 取字节集右边 (封包数据, 取字节集长度 (封包数据) － 3)
        .判断循环首 (取字节集长度 (封包数据) ＞ 0)
            a ＝ 解密到数值 (取字节集左边 (封包数据, 1))  ' 索引位置
            人物数据 [i].钱庄 [a].炼化属性 ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))
            人物数据 [i].钱庄 [a].炼化等级 ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))
            封包数据 ＝ 字节集替换 (封包数据, 1, 9, )
        .判断循环尾 ()
    .默认

    .判断结束


.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 218 })  ' 炼化成功or失败
    ' BA,DA
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))  ' 1=成功 2=失败
    .如果真 (a ＞ 0 且 a ＜ 3)
        b ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))  ' 物品栏位置
        显示信息 (i, “『” ＋ 取物品名 (人物数据 [i].物品栏 [b].id) ＋ “』炼化” ＋ 选择 (a ＝ 1, “成功, 『” ＋ 取炼化属性 (人物数据 [i].物品栏 [b].炼化属性) ＋ “』提升至” ＋ 到文本 (人物数据 [i].物品栏 [b].炼化等级) ＋ “阶.”, “失败,损失了装备耐久度.”), 选择 (a ＝ 1, #品红, #红色))
        .如果真 (配置_炼化装备.开始炼化 且 i ＝ 1)
            .判断开始 (a ＝ 1)
                .判断开始 (人物数据 [i].物品栏 [b].炼化等级 ＜ 配置_炼化装备.炼到多少阶)
                    .判断开始 (配置_炼化装备.道具 ＞ 0 且 人物数据 [i].物品栏 [b].炼化等级 ≥ 配置_炼化装备.多少阶开始用道具)
                        c ＝ 取物品栏物品序号 (i, 配置_炼化装备.灵石)
                        d ＝ 取物品栏物品序号 (i, 配置_炼化装备.道具)
                        .如果真 (c ＝ 0)
                            自动炼化 (i)
                            返回 ()
                        .如果真结束
                        .如果真 (d ＝ 0)
                            显示信息 (i, “物品栏缺少[” ＋ 取物品名 (配置_炼化装备.道具) ＋ “]”, #红色)
                        .如果真结束
                        炼化装备 (i, c, b, d)

                    .默认
                        c ＝ 取物品栏物品序号 (i, 配置_炼化装备.灵石)
                        .如果真 (c ＝ 0)
                            自动炼化 (i)
                            返回 ()
                        .如果真结束
                        炼化装备 (i, c, b, 0)
                    .判断结束

                .默认
                    自动炼化 (i)
                .判断结束


            .判断 (a ＝ 2)
                c ＝ 取物品栏物品序号 (i, 配置_炼化装备.修装材料)
                .如果真 (c ＝ 0)
                    显示信息 (i, “物品栏缺少[” ＋ 取物品名 (配置_炼化装备.修装材料) ＋ “]”, #红色)
                    返回 ()
                .如果真结束
                修理物品 (i, b, c)

            .默认
                显示信息 (i, “自动炼化异常...[” ＋ 到文本 (a) ＋ “]”, #红色)
            .判断结束

        .如果真结束

    .如果真结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 213 })  ' 修理完成
    ' BA,D5
    .如果真 (配置_炼化装备.开始炼化 且 i ＝ 1)
        自动炼化 (i)
    .如果真结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 164 })  ' 失去物品
    ' BA,A4
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))
    b ＝ 解密到数值 (取字节集右边 (封包数据, 1))
    物品_失去 (i, a, b)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 167 })  ' 物品移动
    ' BA,A7
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))  ' 被移动位置
    b ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))  ' 数量
    c ＝ 解密到数值 (取字节集中间 (封包数据, 5, 1))  ' 移动到的位置
    人物数据 [i].物品栏 [a].数量 ＝ 人物数据 [i].物品栏 [a].数量 － b
    人物数据 [i].物品栏 [c].数量 ＝ 人物数据 [i].物品栏 [c].数量 ＋ b
    .如果真 (人物数据 [i].物品栏 [c].id ＝ 0)
        人物数据 [i].物品栏 [c].id ＝ 人物数据 [i].物品栏 [a].id
        人物数据 [i].物品栏 [c].属性 ＝ 人物数据 [i].物品栏 [a].属性
        人物数据 [i].物品栏 [c].属性值 ＝ 人物数据 [i].物品栏 [a].属性值
        人物数据 [i].物品栏 [c].经验值 ＝ 人物数据 [i].物品栏 [a].经验值
        人物数据 [i].物品栏 [c].附加属性 ＝ 人物数据 [i].物品栏 [a].附加属性
        人物数据 [i].物品栏 [c].附加属性值 ＝ 人物数据 [i].物品栏 [a].附加属性值
        人物数据 [i].物品栏 [c].损耗 ＝ 人物数据 [i].物品栏 [a].损耗

    .如果真结束
    .如果真 (人物数据 [i].物品栏 [a].数量 ＝ 0)
        连续赋值 (0, 人物数据 [i].物品栏 [a].id, 人物数据 [i].物品栏 [a].属性, 人物数据 [i].物品栏 [a].属性值, 人物数据 [i].物品栏 [a].经验值, 人物数据 [i].物品栏 [a].附加属性, 人物数据 [i].物品栏 [a].附加属性值, 人物数据 [i].物品栏 [a].损耗)
    .如果真结束
    刷新物品栏列表 (i)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 178 })  ' 摊位
    ' BA,B2,

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 182 })  ' 人物 装备损耗
    ' BA,B6
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))  ' 位置
    b ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))  ' 耗损值
    人物数据 [i].装备 [a].损耗 ＝ b
    显示人物装备 (i)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 177 })  ' 武将 装备损耗
    ' BA,B1
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))  ' 武将序号
    b ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))  ' 装备位置
    c ＝ 解密到数值 (取字节集中间 (封包数据, 5, 1))  ' 耗损值
    人物数据 [i].武将 [a].装备 [b].损耗 ＝ c
    显示武将装备 (i, a)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 142 })  ' 装备损坏
    ' BA,8E
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))  ' 序号 0=自己 >0=武将序号
    b ＝ 解密到数值 (取字节集中间 (封包数据, 4, 2))  ' 装备ID
    c ＝ 取装备位置 (b)
    .判断开始 (a ＝ 0)
        人物数据 [i].装备 [c].id ＝ b
        显示人物装备 (i)

    .默认
        人物数据 [i].武将 [a].装备 [c].id ＝ b
        显示武将装备 (i, a)
    .判断结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 186 })  ' 武将穿上装备
    ' BA,BA,
    a ＝ 解密到数值 (取字节集右边 (封包数据, 1))
    临时装备信息 [1] ＝ 人物数据 [i].物品栏 [a]
    b ＝ 人物数据 [i].物品栏 [a].id
    c ＝ 取装备位置 (b)
    d ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))
    人物数据 [i].物品栏 [a] ＝ 人物数据 [i].武将 [d].装备 [c]
    人物数据 [i].武将 [d].装备 [c] ＝ 临时装备信息 [1]
    刷新物品栏列表 (i)
    显示武将装备 (i, d)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 187 })  ' 武将取下装备
    ' BA,BB,
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))  ' 武将序号
    b ＝ 解密到数值 (取字节集右边 (封包数据, 1))  ' 背包位置
    c ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))  ' 装备位置
    人物数据 [i].物品栏 [b] ＝ 人物数据 [i].武将 [a].装备 [c]
    人物数据 [i].武将 [a].装备 [c] ＝ 临时装备信息 [1]  ' 清理装备
    刷新物品栏列表 (i)
    显示武将装备 (i, a)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 188 })  ' 人物穿上装备
    ' BA,BC,
    a ＝ 解密到数值 (取字节集右边 (封包数据, 1))  ' 背包位置
    b ＝ 人物数据 [i].物品栏 [a].id
    c ＝ 取装备位置 (b)
    临时装备信息 [1] ＝ 人物数据 [i].物品栏 [a]
    人物数据 [i].物品栏 [a] ＝ 人物数据 [i].装备 [c]
    人物数据 [i].装备 [c] ＝ 临时装备信息 [1]
    刷新物品栏列表 (i)
    显示人物装备 (i)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 189 })  ' 人物取下装备
    ' BA,BD,
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))  ' 装备位置
    b ＝ 解密到数值 (取字节集右边 (封包数据, 1))  ' 背包位置
    人物数据 [i].物品栏 [b] ＝ 人物数据 [i].装备 [a]
    人物数据 [i].装备 [a] ＝ 临时装备信息 [1]
    刷新物品栏列表 (i)
    显示人物装备 (i)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 148 })  ' 系统信息
    ' ba,94
    .如果 (取字节集中间 (封包数据, 3, 1) ＝ { 224 })
        ' ba,94,e0
        显示信息 (i, “(系统公告):” ＋ 解密到文本 (取字节集右边 (封包数据, 取字节集长度 (封包数据) － 3)), #红色)
    .否则

    .如果结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 34 })  ' 背包巴豆2放入物品
    ' BA,22
    封包数据 ＝ 取字节集右边 (封包数据, 取字节集长度 (封包数据) － 4)
    .判断循环首 (取字节集长度 (封包数据) ＞ 0)
        a ＝ 解密到数值 (取字节集左边 (封包数据, 1))
        人物数据 [i].背包栏2 [a].id ＝ 解密到数值 (取字节集中间 (封包数据, 2, 2))
        人物数据 [i].背包栏2 [a].数量 ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))
        封包数据 ＝ 取字节集右边 (封包数据, 取字节集长度 (封包数据) － 16)
    .判断循环尾 ()
    刷新背包栏列表 (i)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 157 })  ' 背包巴豆放入物品
    ' ba,9d
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 2))
    b ＝ 解密到数值 (取字节集中间 (封包数据, 5, 1))
    背包物品_获得 (i, a, b)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 156 } 或 取字节集中间 (封包数据, 2, 1) ＝ { 61 })  ' 背包巴豆取出物品
    ' ba,9c  ba,3d
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))
    .判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 156 })
        人物数据 [i].背包栏 [a] ＝ 临时装备信息 [1]
    .默认
        人物数据 [i].背包栏2 [a] ＝ 临时装备信息 [1]
    .判断结束
    刷新背包栏列表 (i)
.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 40 })  ' 畅游卡到期时间
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))
    .判断开始 (a ＝ 1)
        封包数据 ＝ 解密到字节集 (取字节集右边 (封包数据, 8))
        显示信息 (i, “畅游卡到期时间:” ＋ 到文本 (取字节集数据 (封包数据, #日期时间型, )), #品红)
    .判断 (a ＝ 2)
        显示信息 (i, “已达最高上限.”, #红色)
    .默认

    .判断结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 42 })  ' 畅游卡到期时间
    ' ba,28  BA,2A
    封包数据 ＝ 解密到字节集 (取字节集右边 (封包数据, 8))
    显示信息 (i, “畅游卡到期时间2:” ＋ 到文本 (取字节集数据 (封包数据, #日期时间型, )), #品红)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 228 })  ' BA,E4 人物0装增加经验
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))
    b ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))  ' 装备
    c ＝ 解密到数值 (取字节集右边 (封包数据, 4))  ' 经验值
    .判断开始 (a ＝ 0)
        显示信息 (i, 取物品名 (人物数据 [i].装备 [b].id, 真) ＋ “ Exp+” ＋ 到文本 (c － 人物数据 [i].装备 [b].经验值))

        人物数据 [i].装备 [b].经验值 ＝ c
    .默认

    .判断结束

.默认

.判断结束


.子程序 _A8, , , 加载人物个人数据
.参数 i, 整数型
.参数 封包数据, 字节集

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 169 })  ' A9
    .如果真 (人物数据 [i].正在处理事件)
        左键点击 (i)
    .如果真结束
    人物数据 [i].正在切图 ＝ 假

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 174 })  ' AE
    加载人物 (i, 封包数据)
.默认

.判断结束


.子程序 _8A, , , 兵团相关
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 172 })
    ' 8A,AC

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 175 })
    ' 8A,AF

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 174 })  ' 邀请入团
    ' 8A,AE
    a ＝ 解密到数值 (取字节集右边 (封包数据, 4))
    显示信息 (i, 取玩家名 (i, a, 真) ＋ “ 邀请加入兵团.”, #品红)
    .判断开始 (主窗口.自动加入兵团.选中)
        发送封包 (i, 十六到字节集 (“59,E9,AA,AD,8A,AE,” ＋ 数值加密 (a, 4) ＋ “AC”))
        显示信息 (i, “同意加入.”, #品红)
    .默认
        发送封包 (i, 十六到字节集 (“59,E9,AA,AD,8A,AE,” ＋ 数值加密 (a, 4) ＋ “AF”))
        显示信息 (i, “拒绝加入.”, #品红)
    .判断结束
    ' 59,E9,AA,AD,8A,AE,0A,99,AD,AD,AC, 同意入团
.默认

.判断结束


.子程序 _BD, , , 北斗答题
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型
.局部变量 b, 整数型

a ＝ 解密到数值 (取字节集中间 (封包数据, 2, 1))
.判断开始 (a ＝ 1)  ' AC  北斗问题
    人物数据 [i].北斗答题 ＝ 真
    北斗处理 (i, 封包数据)

.判断 (a ＝ 2)
    b ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))
    显示信息 (i, 多项选择 (b, “真聪明..恭喜你答对了!!”, “喔喔~~你答错了喔”, “喔喔~~你错三次了喔”), #蓝色)
.默认

.判断结束


.子程序 _BE, , , 武将相关
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 172 })  ' 武将出战
    ' be,ac
    a ＝ 解密到数值 (取字节集右边 (封包数据, 4))
    出战武将信息 (i, a)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 175 })
    ' be,ac
    显示信息 (i, “武将休息.”, #蓝色)
    出战武将信息 (i, 0)
    刷新武将信息 (i, 0)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 169 })
    ' be,a9
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 2))
    出战武将信息 (i, a)
    刷新武将信息 (i, a)
.默认

.判断结束


.子程序 _A5, , , 属性相关
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型
.局部变量 b, 整数型
.局部变量 c, 整数型
.局部变量 值, 整数型
.局部变量 x, 整数型
.局部变量 y, 整数型

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 172 })  ' 十六到字节集 (“AC”))  ' 角色
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))  ' 类别
    b ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))  ' + -
    值 ＝ 解密到数值 (取字节集中间 (封包数据, 5, 4))
    .判断开始 (a ＝ 25)
        人物数据 [i].当前hp ＝ 值
        显示人物HP (i)
        .如果真 (值 ＜ 1)
            人物数据 [i].死亡次数 ＝ 人物数据 [i].死亡次数 ＋ 1
            显示信息 (i, “你被打死了,自登陆以来总共被打死[” ＋ 到文本 (人物数据 [i].死亡次数) ＋ “]次!”, #红色)
            关闭机关盒 (i)
            打开机关盒 (i)
        .如果真结束

    .判断 (a ＝ 26)
        人物数据 [i].当前sp ＝ 值
        显示人物SP (i)
        .如果真 (人物数据 [i].正在战斗)
            x ＝ 人物数据 [i].战场位置.x ＋ 1
            y ＝ 人物数据 [i].战场位置.y ＋ 1
            .计次循环首 (5, a)
                人物数据 [a].战场 [x] [y].当前sp ＝ 值
            .计次循环尾 ()
        .如果真结束

    .判断 (a ＝ 27)  ' int
        人物数据 [i].智力 ＝ 值
    .判断 (a ＝ 28)  ' atk
        人物数据 [i].攻击 ＝ 值
    .判断 (a ＝ 29)  ' def
        人物数据 [i].防御 ＝ 值
    .判断 (a ＝ 30)  ' agi
        人物数据 [i].敏捷 ＝ 值
    .判断 (a ＝ 31)  ' hpx
        人物数据 [i].体质 ＝ 值
    .判断 (a ＝ 32)  ' spx
        人物数据 [i].能量 ＝ 值
    .判断 (a ＝ 110)
        值 ＝ 解密到数值 (取字节集右边 (封包数据, 4))
        显示信息 (i, “习得技能:” ＋ 取技能名 (值, 真), #蓝色)

    .判断 (a ＝ 35)
        人物数据 [i].等级 ＝ 值
        显示信息 (i, 人物数据 [i].名字 ＋ “[” ＋ 到文本 (人物数据 [i].id) ＋ “] 升级!”, #墨绿)
        刷新人物显示 (i)

    .判断 (a ＝ 36)
        人物数据 [i].上场战斗经验 ＝ 值 － 人物数据 [i].经验值
        .如果真 (个人配置 [i].显示经验 ＝ 真)
            显示信息 (i, 人物数据 [i].名字 ＋ “ 获得经验:” ＋ 到文本 (人物数据 [i].上场战斗经验), #墨绿)
        .如果真结束
        人物数据 [i].经验值 ＝ 值
        显示人物经验 (i)

    .判断 (a ＝ 37)
        人物数据 [i].剩余技能点 ＝ 值

    .判断 (a ＝ 38)
        人物数据 [i].剩余属性点 ＝ 值

    .判断 (a ＝ 50)  ' 阵营点
        ' 取第9位=魏蜀吴

    .判断 (a ＝ 62)  ' 经验条下面的 星星数

    .判断 (a ＝ 69)  ' 经验条下面的 星星数
        显示信息 (i, “本命灯数量x” ＋ 到文本 (值), #品红)

    .判断 (a ＝ 207)
        ' 人物数据 [i].附加hp ＝ 多项选择 (b, 值, －值)
        ' 显示人物HP (i)

    .判断 (a ＝ 208)
        ' 人物数据 [i].附加sp ＝ 多项选择 (b, 值, －值)
        ' 显示人物SP (i)

    .判断 (a ＝ 209)

    .判断 (a ＝ 210)
        人物数据 [i].附加攻击 ＝ 多项选择 (b, 值, －值)

    .判断 (a ＝ 211)
        人物数据 [i].附加防御 ＝ 多项选择 (b, 值, －值)

    .判断 (a ＝ 212)
        人物数据 [i].附加智力 ＝ 多项选择 (b, 值, －值)

    .判断 (a ＝ 213)

    .判断 (a ＝ 214)
        人物数据 [i].附加敏捷 ＝ 多项选择 (b, 值, －值)

    .判断 (b ＝ 215)
        ' 人物数据 [i].附加hp ＝ 多项选择 (b, 值, －值)
        ' 显示人物HP (i)

    .判断 (b ＝ 216)
        ' 人物数据 [i].附加sp ＝ 多项选择 (b, 值, －值)
        ' 显示人物SP (i)

    .判断 (a ＝ 218)
        人物数据 [i].附加体质 ＝ 多项选择 (b, 值, －值)

    .判断 (a ＝ 219)
        人物数据 [i].附加能量 ＝ 多项选择 (b, 值, －值)
    .默认

    .判断结束
    刷新人物信息 (i)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 175 })  ' 十六到字节集 (“AF”))  ' 武将
    a ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))  ' 武将索引号
    b ＝ 解密到数值 (取字节集中间 (封包数据, 6, 1))  ' 类别
    c ＝ 解密到数值 (取字节集中间 (封包数据, 7, 1))  ' + -
    值 ＝ 解密到数值 (取字节集中间 (封包数据, 8, 4))  ' 值
    .判断开始 (b ＝ 25)  ' HP
        人物数据 [i].武将 [a].当前hp ＝ 值
        显示武将HP (i, a)
        .如果真 (值 ＜ 1)
            人物数据 [i].死亡次数_武将 ＝ 人物数据 [i].死亡次数_武将 ＋ 1
            显示信息 (i, “你的武将被打死了,自登陆以来总共被打死[” ＋ 到文本 (人物数据 [i].死亡次数_武将) ＋ “]次!”, #红色)
            关闭机关盒 (i)
            打开机关盒 (i)
        .如果真结束

    .判断 (b ＝ 26)  ' SP
        人物数据 [i].武将 [a].当前sp ＝ 值
        显示武将SP (i, a)
        .如果真 (人物数据 [i].正在战斗)
            x ＝ 多项选择 (人物数据 [i].战场位置.x ＋ 1, 1, 0, 3, 2) ＋ 1
            y ＝ 人物数据 [i].战场位置.y ＋ 1
            .计次循环首 (5, a)
                人物数据 [a].战场 [x] [y].当前sp ＝ 值
            .计次循环尾 ()
        .如果真结束


    .判断 (b ＝ 27)  ' int
        .如果真 (人物数据 [i].武将 [a].智力 ＞ 0)
            显示信息 (i, 人物数据 [i].武将 [a].名字 ＋ “ INT+” ＋ 到文本 (值 － 人物数据 [i].武将 [a].智力), #蓝色)
        .如果真结束
        人物数据 [i].武将 [a].智力 ＝ 值

    .判断 (b ＝ 28)  ' atk
        .如果真 (人物数据 [i].武将 [a].攻击 ＞ 0)
            显示信息 (i, 人物数据 [i].武将 [a].名字 ＋ “ ATK+” ＋ 到文本 (值 － 人物数据 [i].武将 [a].攻击), #蓝色)
        .如果真结束
        人物数据 [i].武将 [a].攻击 ＝ 值

    .判断 (b ＝ 29)  ' def
        .如果真 (人物数据 [i].武将 [a].防御 ＞ 0)
            显示信息 (i, 人物数据 [i].武将 [a].名字 ＋ “ DEF+” ＋ 到文本 (值 － 人物数据 [i].武将 [a].防御), #蓝色)
        .如果真结束
        人物数据 [i].武将 [a].防御 ＝ 值

    .判断 (b ＝ 30)  ' agi
        .如果真 (人物数据 [i].武将 [a].敏捷 ＞ 0)
            显示信息 (i, 人物数据 [i].武将 [a].名字 ＋ “ AGI+” ＋ 到文本 (值 － 人物数据 [i].武将 [a].敏捷), #蓝色)
        .如果真结束
        人物数据 [i].武将 [a].敏捷 ＝ 值

    .判断 (b ＝ 31)  ' hpx
        .如果真 (人物数据 [i].武将 [a].体质 ＞ 0)
            显示信息 (i, 人物数据 [i].武将 [a].名字 ＋ “ HPX+” ＋ 到文本 (值 － 人物数据 [i].武将 [a].体质), #蓝色)
        .如果真结束
        人物数据 [i].武将 [a].体质 ＝ 值

    .判断 (b ＝ 32)  ' spx
        .如果真 (人物数据 [i].武将 [a].能量 ＞ 0)
            显示信息 (i, 人物数据 [i].武将 [a].名字 ＋ “ SPX+” ＋ 到文本 (值 － 人物数据 [i].武将 [a].能量), #蓝色)
        .如果真结束
        人物数据 [i].武将 [a].能量 ＝ 值

    .判断 (b ＝ 35)  ' 等级
        人物数据 [i].武将 [a].等级 ＝ 值
        显示信息 (i, 人物数据 [i].武将 [a].名字 ＋ “[” ＋ 到文本 (人物数据 [i].武将 [a].id) ＋ “] 升级!”, #墨绿)
        刷新武将显示 (i)

    .判断 (b ＝ 36)  ' 经验
        人物数据 [i].武将 [a].上场战斗经验 ＝ 值 － 人物数据 [i].武将 [a].经验值
        .如果真 (个人配置 [i].显示经验 ＝ 真)
            显示信息 (i, 人物数据 [i].武将 [a].名字 ＋ “ 获得经验:” ＋ 到文本 (人物数据 [i].武将 [a].上场战斗经验), #墨绿)
        .如果真结束
        人物数据 [i].武将 [a].经验值 ＝ 值
        显示武将经验 (i, a)

    .判断 (b ＝ 37)  ' 剩余技能点
        人物数据 [i].武将 [a].剩余技能点 ＝ 值

    .判断 (b ＝ 64)  ' 忠诚
        人物数据 [i].武将 [a].忠诚 ＝ 值

    .判断 (b ＝ 110)  ' 技能等级调整
        c ＝ 解密到数值 (取字节集中间 (封包数据, 12, 2))
        显示信息 (i, 人物数据 [i].武将 [a].名字 ＋ “(” ＋ 到文本 (人物数据 [i].武将 [a].id) ＋ “) 的技能 ” ＋ 取技能名 (c, 真) ＋ “ 已加到[ ” ＋ 到文本 (值) ＋ “ ]级”, #蓝色)

    .判断 (b ＝ 207)
        ' 人物数据 [i].武将 [a].附加hp ＝ 多项选择 (c, 值, －值)
        ' 显示武将HP (i, a)

    .判断 (b ＝ 208)
        ' 人物数据 [i].武将 [a].附加sp ＝ 多项选择 (c, 值, －值)
        ' 显示武将SP (i, a)

    .判断 (b ＝ 209)
        显示信息 (i, “警告:武将附加属性 209=” ＋ 到文本 (多项选择 (c, 值, －值)), #红色)

    .判断 (b ＝ 210)
        人物数据 [i].武将 [a].附加攻击 ＝ 多项选择 (c, 值, －值)

    .判断 (b ＝ 211)
        人物数据 [i].武将 [a].附加防御 ＝ 多项选择 (c, 值, －值)

    .判断 (b ＝ 212)
        人物数据 [i].武将 [a].附加智力 ＝ 多项选择 (c, 值, －值)

    .判断 (b ＝ 213)
        显示信息 (i, “警告:武将附加属性 213=” ＋ 到文本 (多项选择 (c, 值, －值)), #红色)

    .判断 (b ＝ 214)
        人物数据 [i].武将 [a].附加敏捷 ＝ 多项选择 (c, 值, －值)
    .判断 (b ＝ 215)
        ' 人物数据 [i].武将 [a].附加hp ＝ 多项选择 (c, 值, －值)
        ' 显示武将HP (i, a)

    .判断 (b ＝ 216)
        ' 人物数据 [i].武将 [a].附加sp ＝ 多项选择 (c, 值, －值)
        ' 显示武将SP (i, a)

    .判断 (b ＝ 218)
        人物数据 [i].武将 [a].附加体质 ＝ 多项选择 (c, 值, －值)
    .判断 (b ＝ 219)
        人物数据 [i].武将 [a].附加能量 ＝ 多项选择 (c, 值, －值)
    .默认

    .判断结束
    刷新武将信息 (i, a)
.默认

.判断结束



.子程序 _EC, , , 机关盒
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 c, 整数型
.局部变量 d, 整数型
.局部变量 a, 整数型
.局部变量 b, 整数型

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 172 })
    ' ec,ac
    .如果真 (人物数据 [i].机关盒 ＝ 假)
        显示信息 (i, “机关盒已打开.”, #蓝色)
        人物数据 [i].机关盒 ＝ 真
    .如果真结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 175 })
    ' ec,af
    .如果真 (人物数据 [i].机关盒 ＝ 真)
        人物数据 [i].机关盒 ＝ 假
        显示信息 (i, “机关盒已关闭.”, #红色)
    .如果真结束
    .如果真 (主窗口.打开机关盒.选中)
        打开机关盒 (i)
    .如果真结束

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 171 })  ' 晶石
    ' ec,ab
    c ＝ 取字节集长度 (封包数据) － 2
    .计次循环首 (c ÷ 5, d)
        a ＝ 解密到数值 (取字节集中间 (封包数据, d × 5 － 3 ＋ 2, 2))  ' 晶石ID
        b ＝ 解密到数值 (取字节集中间 (封包数据, d × 5 － 1 ＋ 2, 2))  ' 晶石数量
        显示信息 (i, 取物品名 (a, 真) ＋ “ 可用次数:” ＋ 到文本 (b), #黑色)
    .计次循环尾 ()
.默认

.判断结束


.子程序 _99, , , 回合开始
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 临时战斗选择, 数据类型_已选择目标

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 172 } 且 人物数据 [i].正在战斗 ＝ 真)  ' AC
    已选择目标 [i] ＝ 临时战斗选择
    已选择目标 [i ＋ 5] ＝ 临时战斗选择
    人物数据 [i].战斗回合数 ＝ 人物数据 [i].战斗回合数 ＋ 1
    .如果真 (i ＝ 1)
        .如果真 (人物数据 [i].战斗回合数 ＝ 1)
            战场_重绘 ()
        .如果真结束
        战斗总伤害 ＝ 0
        显示战斗信息 (“回合: ” ＋ 到文本 (人物数据 [i].战斗回合数), #墨绿)
    .如果真结束
    战斗_检测 (i)
.默认

.判断结束



.子程序 _9F
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型
.局部变量 b, 整数型

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 172 })  ' AC
    封包数据 ＝ 字节集替换 (封包数据, 1, 2, )
    .循环判断首 ()
        a ＝ 解密到数值 (取字节集左边 (封包数据, 2))
        战斗总伤害 ＝ 战斗总伤害 ＋ 战斗_处理 (i, 取字节集中间 (封包数据, 3, a))
        封包数据 ＝ 字节集替换 (封包数据, 1, a ＋ 2, )
        b ＝ b ＋ 1
    .循环判断尾 (封包数据 ≠ {  })
    .如果真 (i ＝ 1)
        战场_重绘 ()
        .如果真 (b ＞ 1)
            显示战斗信息 (到文本 (b) ＋ “ 人合击,总伤害: ” ＋ 到文本 (战斗总伤害), #品红)
        .如果真结束
        显示战斗信息 (“- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -”)

    .如果真结束

.默认

.判断结束


.子程序 _98
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型
.局部变量 b, 整数型
.局部变量 c, 整数型

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 174 })  ' AE
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1)) ＋ 1
    b ＝ 解密到数值 (取字节集右边 (封包数据, 1)) ＋ 1
    战场_离开 (i, a, b)

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 168 })  ' A8 选择攻击完毕 长时间没有准备的有蜗牛提示

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 172 })  ' AC 状态解除
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1)) ＋ 1
    b ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1)) ＋ 1
    c ＝ 解密到数值 (取字节集中间 (封包数据, 5, 1))
    人物数据 [i].战场 [a] [b].BUFF [c] ＝ 假
    人物数据 [i].战场 [a] [b].BUFFID ＝ 0
    .如果真 (i ＝ 1)
        显示战斗信息 (人物数据 [i].战场 [a] [b].名称 ＋ “(” ＋ 到文本 (a) ＋ “,” ＋ 到文本 (b) ＋ “)” ＋ “ ” ＋ 多项选择 (c, “定身状态”, “增益状态”, “减益状态”, “”, “特殊状态”) ＋ “ 解除.”, #蓝色)
    .如果真结束

.默认

.判断结束


.子程序 _87
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 169 })  ' A9 吃领悟丹
    a ＝ 解密到数值 (取字节集右边 (封包数据, 1))
    显示信息 (i, “使用物品领悟丹成功_” ＋ 多项选择 (a, “知力”, “攻击力”, “防御力”, “敏捷”, “体质”, “能量”), #蓝色)
.默认

.判断结束


.子程序 _E4, , , 打BOSS
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型
.局部变量 b, 整数型
.局部变量 封包类, 整数型
.局部变量 c, 整数型

封包类 ＝ 解密到数值 (取字节集中间 (封包数据, 2, 1))
.判断开始 (封包类 ＝ 4)  ' A9
    ' a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 4))
    ' b ＝ 解密到数值 (取字节集右边 (封包数据, 1))
    ' 显示信息 (i, “本次伤害:” ＋ 到文本 (a) ＋ “ 剩余回合:” ＋ 到文本 (b), #品红)

.判断 (封包类 ＝ 1)  ' AC BOSS活动显示
    a ＝ 解密到数值 (取字节集右边 (封包数据, 1))
    .判断开始 (a ＝ 1)
        显示信息 (i, “世界BOSS挑战已经开始.”, #红色)
    .判断 (a ＝ 2)
        显示信息 (i, “今日世界BOSS挑战已经结束.”, #红色)
    .判断 (a ＝ 3)
        b ＝ 解密到数值 (取字节集中间 (封包数据, 5, 1))
        显示信息 (i, “伤害倍数 x ” ＋ 到文本 (b), #品红)
    .默认

    .判断结束

.判断 (封包类 ＝ 3)  ' AE 战斗后显示的总伤害和单次伤害
    a ＝ 解密到数值 (取字节集中间 (封包数据, 3, 1))
    .判断开始 (a ＝ 1)  ' 单次伤害
        显示信息 (i, “本次伤害: ” ＋ 到文本 (解密到数值 (取字节集右边 (封包数据, 4))), #品红)
    .判断 (a ＝ 3)
        显示信息 (i, “今日累积伤害: ” ＋ 到文本 (解密到数值 (取字节集右边 (封包数据, 4))), #品红)
    .默认

    .判断结束

.判断 (封包类 ＝ 2)
    .判断开始 (取字节集中间 (封包数据, 3, 1) ＝ { 172 })  ' E4,AF,AC 单次排行
        封包数据 ＝ 字节集替换 (封包数据, 1, 11, )
        显示信息 (i, “世界BOSS单次排行”)

    .判断 (取字节集中间 (封包数据, 3, 1) ＝ { 169 })  ' E4,AF,A9 累积排行
        封包数据 ＝ 字节集替换 (封包数据, 1, 11, )
        显示信息 (i, “世界BOSS累积排行”)

    .默认
        返回 ()
    .判断结束

    .判断循环首 (取字节集长度 (封包数据) ≥ 20)
        a ＝ 解密到数值 (取字节集左边 (封包数据, 1))
        b ＝ 解密到数值 (取字节集中间 (封包数据, 16, 1))
        c ＝ 解密到数值 (取字节集中间 (封包数据, 17, 4))
        显示信息 (i, 调整名字长度 (解密到文本 (取字节集中间 (封包数据, 2, a)), 14) ＋ 字符 (9) ＋ 到文本 (b) ＋ 字符 (9) ＋ 到文本 (c))
        封包数据 ＝ 字节集替换 (封包数据, 1, 20, )
    .判断循环尾 ()

.默认

.判断结束


.子程序 _B6
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 172 })  ' ac
    a ＝ 解密到数值 (取字节集右边 (封包数据, 1))
    .判断开始 (a ＝ 0)
        显示信息 (i, “购买物品成功.”, #蓝色)
    .判断 (a ＝ 1)
        显示信息 (i, “购买物品失败,金币不足.”, #红色)
    .判断 (a ＝ 2)
        显示信息 (i, “购买物品失败,物品栏已经满了.”, #红色)
    .判断 (a ＝ 3)
        显示信息 (i, “购买物品失败,物品栏放不下你买的数量.”, #红色)
    .判断 (a ＝ 4)
        显示信息 (i, “交易失败”, #红色)
    .默认

    .判断结束

.默认

.判断结束


.子程序 _B0, , , 钱庄打开
.参数 i, 整数型
.参数 封包数据, 字节集

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ { 171 })  ' AB 存取物品
    显示信息 (i, “钱庄已打开.”, #蓝色)
    刷新钱庄物品列表 (i)
    刷新钱庄按钮 ()

.判断 (取字节集中间 (封包数据, 2, 1) ＝ { 168 })  ' 金币

.默认

.判断结束


.子程序 _8B
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 类型, 整数型
.局部变量 a, 整数型

类型 ＝ 解密到数值 (取字节集中间 (封包数据, 2, 1))
.判断开始 (类型 ＝ 1)  ' 打坐
    a ＝ 解密到数值 (取字节集右边 (封包数据, 4))
    显示信息 (i, “修行经验提升 : ” ＋ 到文本 (a), #墨绿)
.默认

.判断结束


.子程序 _E3
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 lv
.局部变量 result

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ 十六到字节集 (“AC”))  ' 本命灯等级
    lv ＝ 解密到数值 (取字节集中间 (封包数据, 4, 1))  ' 体质
    .判断开始 (lv ＞ 5)
        result ＝ (lv － 5) × 5 ＋ 15
    .默认
        result ＝ lv × (lv ＋ 1) ÷ 2
    .判断结束
    人物数据 [i].本命灯.体质 ＝ result

    lv ＝ 解密到数值 (取字节集中间 (封包数据, 5, 1))  ' 防御
    人物数据 [i].本命灯.防御 ＝ lv × (lv ＋ 1) ÷ 2

    lv ＝ 解密到数值 (取字节集中间 (封包数据, 6, 1))  ' 智力
    人物数据 [i].本命灯.智力 ＝ lv × (lv ＋ 1) ÷ 2

    lv ＝ 解密到数值 (取字节集中间 (封包数据, 7, 1))  ' 能量
    .判断开始 (lv ＞ 5)
        result ＝ (lv － 5) × 5 ＋ 15
    .默认
        result ＝ lv × (lv ＋ 1) ÷ 2
    .判断结束
    人物数据 [i].本命灯.能量 ＝ result

    lv ＝ 解密到数值 (取字节集中间 (封包数据, 8, 1))  ' 敏捷
    .判断开始 (lv ＞ 5)
        result ＝ (lv － 5) × 5 ＋ 15
    .默认
        result ＝ lv × (lv ＋ 1) ÷ 2
    .判断结束
    人物数据 [i].本命灯.敏捷 ＝ result

    lv ＝ 解密到数值 (取字节集中间 (封包数据, 9, 1))  ' 攻击
    人物数据 [i].本命灯.攻击 ＝ lv × (lv ＋ 1) ÷ 2


    lv ＝ 解密到数值 (取字节集中间 (封包数据, 10, 1))  ' 暴击
    人物数据 [i].本命灯.爆击 ＝ 5 ＋ (lv － 5) × 2

.默认

.判断结束


.子程序 _92, , , 战役相关
.参数 i, 整数型
.参数 封包数据, 字节集

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ 十六到字节集 (“AB”))
    显示信息 (i, “个人战功: ” ＋ 到文本 (解密到数值 (取字节集右边 (封包数据, 4))), #墨绿)
.默认

.判断结束


.子程序 _E5, , , 转转乐
.参数 i, 整数型
.参数 封包数据, 字节集
.局部变量 a, 整数型

' 59,E9,AF,AD,E5,A9 投币
' 59,E9,AF,AD,E5,AF 转动
' 59,E9,AF,AD,E5,AE 加倍转动

.判断开始 (取字节集中间 (封包数据, 2, 1) ＝ 十六到字节集 (“AC”))  ' 奖励明细

.判断 (取字节集中间 (封包数据, 2, 1) ＝ 十六到字节集 (“AB”))
    a ＝ 解密到数值 (取字节集右边 (封包数据, 2))
    人物数据 [i].转转币 ＝ a
    显示信息 (i, “已投欢乐币剩余数量: ” ＋ 到文本 (a), #品红)
.默认

.判断结束

